apply from: "${project.rootDir}/androidLib.gradle"

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation parent.ext.kotlin
    implementation parent.ext.appCompat
    implementation parent.ext.recyclerView

    implementation 'junit:junit:4.13.2'
    implementation 'org.hamcrest:hamcrest-library:2.2'
    implementation parent.ext.mockitoAndroid

    def androidTestExclusions = { lib, pairs ->
        implementation(lib) {
            pairs.each { exclude group: "${it.first}", module: "${it.second}" }
        }
    }

    List exclusions = [
            new Tuple2('androidx.appcompat', 'appcompat'),
            new Tuple2('androidx.core', 'core'),
            new Tuple2('androidx.activity', 'activity'),
            new Tuple2('androidx.fragment', 'fragment'),
            new Tuple2('androidx.loader', 'loader'),
            new Tuple2('androidx.recyclerview', 'recyclerview'),
            new Tuple2('androidx.viewpager2', 'viewpager2'),
            new Tuple2('androidx.savedstate', 'savedstate'),
            new Tuple2('androidx.lifecycle', 'lifecycle-livedata'),
            new Tuple2('androidx.lifecycle', 'lifecycle-common'),
            new Tuple2('androidx.lifecycle', 'lifecycle-viewmodel'),
            new Tuple2('androidx.lifecycle', 'lifecycle-livedata-core'),
            new Tuple2('androidx.lifecycle', 'lifecycle-runtime'),
            new Tuple2('androidx.arch.core', 'core-runtime'),
            new Tuple2('com.google.android.material', 'material')
    ]

    androidTestExclusions('androidx.test:runner:1.3.0', exclusions)
    androidTestExclusions('androidx.test:rules:1.4.0', exclusions)
    androidTestExclusions('androidx.test.espresso:espresso-core:3.3.0', exclusions)
    androidTestExclusions('androidx.test.espresso:espresso-contrib:3.3.0', exclusions)
    androidTestExclusions('androidx.test.espresso:espresso-intents:3.3.0', exclusions)
}